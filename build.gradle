plugins {
	id "fabric-loom" version "$loom_version"
	id "legacy-looming" version "$legacy_looming_version"
	id "maven-publish"
}

version = "$mod_version+$target_version"
group = maven_group

base {
	archivesName = archives_name
}

repositories {
	maven { url "https://jitpack.io" }
	flatDir { dirs "libs" }
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings(legacy.yarn(minecraft_version, yarn_build))
	modImplementation "net.fabricmc:fabric-loader:$loader_version"

	include(modImplementation "com.github.tildejustin:Fabric-ASM:$fabric_asm_version")
	include(implementation "org.zeroturnaround:zt-zip:$project.zt_zip_version")
	include "org.slf4j:slf4j-simple:$slf4j_version"
	include "org.slf4j:slf4j-api:$slf4j_version"
	include(implementation "org.apache.commons:commons-lang3:$commons_lang3_version")
	include(implementation "commons-io:commons-io:$commons_io_version")
	include(implementation "commons-codec:commons-codec:$commons_codec_version")
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "version": version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release.set(8)
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
	from("LICENSE") {
		rename { "${it}_$base.archivesName" }
	}
}

publishing {
	publications.create("mavenJava", MavenPublication) {
		from components.java
	}
	repositories {}
}
